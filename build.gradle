plugins {
	id 'java'
	id 'org.springframework.boot' version '3.4.12' apply false
	id 'io.spring.dependency-management' version '1.1.7' apply false
	id 'com.diffplug.spotless' version '6.25.0' apply false
}

group = 'com.study'
version = '0.0.1-SNAPSHOT'
description = 'Webflux Multi-Module Project'

subprojects {
	apply plugin: 'java'
	apply plugin: 'org.springframework.boot'
	apply plugin: 'io.spring.dependency-management'
	apply plugin: 'com.diffplug.spotless'

	java {
		toolchain {
			languageVersion = JavaLanguageVersion.of(21)
		}
	}

	configurations {
		compileOnly {
			extendsFrom annotationProcessor
		}
	}

	repositories {
		mavenCentral()
	}

	spotless {
		java {
			target 'src/**/*.java'

			toggleOffOn()

			// 1단계: Import 정리
			importOrder('java|javax', 'lombok', 'org.springframework', '', '\\#')
			removeUnusedImports()

			// 2단계: 코드 스타일 포맷팅
			eclipse().configFile(rootProject.file('config/formatter/eclipse-formatter.xml'))

			// 3단계: 후처리
			trimTrailingWhitespace()
			endWithNewline()
			indentWithTabs()

			custom 'Record closing paren', { content ->
				content.replaceAll(/(?m)^(public record \w+\([^)]+)(\)\s*\{)/, '$1\n) {')
			}
		}
	}

	tasks.named('check') {
		dependsOn 'spotlessCheck'
	}
}
